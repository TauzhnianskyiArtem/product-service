plugins {
    id 'java'
    id "org.openapi.generator" version '7.1.0'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
}

apply from: 'version.gradle'

group = 'com.iprody.product.service'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.openapitools:jackson-databind-nullable:${versions.jacksonNullable}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${versions.springdocOpenApi}"
    implementation "org.apache.httpcomponents.client5:httpclient5:${versions.httpclient5}"
    compileOnly "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "io.cucumber:cucumber-spring:${versions.cucumber}"
    testImplementation "io.cucumber:cucumber-java:${versions.cucumber}"
    testImplementation "io.cucumber:cucumber-junit:${versions.cucumber}"
    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/main/java"
        }
    }
}

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("$projectDir/specs/openapi.json")
    outputDir.set("$buildDir/generated")
    apiPackage.set("com.iprody.product.service.e2e.generated.api")
    invokerPackage.set("com.iprody.product.service.e2e.generated.invoker")
    modelPackage.set("com.iprody.product.service.e2e.generated.model")
    generateApiTests.set(false)
    generateModelTests.set(false)
    configOptions.set([
            dateLibrary                   : "java21",
            library                       : "resttemplate",
            interfaceOnly                 : "true",
            useJakartaEe                  : "true",
            generateClientAsBean          : "true",
            additionalModelTypeAnnotations: "@lombok.AllArgsConstructor; @lombok.Builder"
    ])
    typeMappings = [
            Date: "Instant"
    ]
    importMappings = [
            "java.util.Date": "java.time.Instant"
    ]
}

compileJava.dependsOn tasks.named("openApiGenerate")
